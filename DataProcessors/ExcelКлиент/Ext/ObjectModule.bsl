
Перем ПроксиКомпоненты;


///////////////////////////////////////////////////////////////////////////////
// ОСНОВНАЯ ФУНКЦИОНАЛЬНОСТЬ

#Область ОсновнаяФункциональность

Функция Открыть(ИмяФайла, ОписаниеОшибки = Неопределено) Экспорт
	
	Токен = Неопределено;
	
	ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	Если ПроксиКомпоненты <> Неопределено Тогда
		Ответ = ПроксиКомпоненты.open(ИмяФайла);
		Если Ответ.error = Истина Тогда
			ОписаниеОшибки = Ответ.description;	
		Иначе
			Токен = Ответ.object;	
		КонецЕсли;
	КонецЕсли;	
	
	Возврат Токен;
	
КонецФункции

Функция Закрыть(Токен, ОписаниеОшибки = Неопределено) Экспорт
	
	Результат = Ложь;
	
	ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	Если ПроксиКомпоненты <> Неопределено Тогда
		Ответ = ПроксиКомпоненты.close(Токен);
		Если Ответ.error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		Иначе
			Результат = Истина;			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция Сохранить(Токен, ОписаниеОшибки = Неопределено) Экспорт
	
	Результат = Ложь;
	
	ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	Если ПроксиКомпоненты <> Неопределено Тогда
		Ответ = ПроксиКомпоненты.save(Токен);
		Если Ответ.error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		Иначе
			Результат = Истина;			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция УстановитьФормулу(Токен, ИндексЛиста, Строка, Столбец, Формула, ОписаниеОшибки = Неопределено) Экспорт
	
	Результат = Ложь;
	
	ПроксиКомпоненты = ПроксиКомпоненты(ОписаниеОшибки);
	Если ПроксиКомпоненты <> Неопределено Тогда
		ЯчейкаXDTO = ПроксиКомпоненты.ФабрикаXDTO.Создать("http://excel.ak.ru/", "excelCell");
		ЯчейкаXDTO.r = Строка;
		ЯчейкаXDTO.c = Столбец;
		
		Ответ = ПроксиКомпоненты.setFormula(Токен, ИндексЛиста, ЯчейкаXDTO, Формула);
		Если Ответ.error = Истина Тогда
			ОписаниеОшибки = Ответ.description;
		Иначе
			Результат = Истина;			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

Функция ПроксиКомпоненты(ОписаниеОшибки = Неопределено)
	
	Если ПроксиКомпоненты = Неопределено Тогда
		ПроксиКомпоненты = КомпонентыJavaПовтИсп.ПроксиКомпоненты(
			ПредопределенноеЗначение("Справочник.КомпонентыJava.excelclient"), ОписаниеОшибки);
	КонецЕсли;
	
	Возврат ПроксиКомпоненты;
	
КонецФункции

#КонецОбласти